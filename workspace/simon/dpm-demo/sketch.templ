dtmc

// timing:
// tick-0:
//     queue state is observed and state change is planned (pm)
//     request are generated (if service requester is active)
// tick-1:
//     requests are served
//     state change is executed
//     service requester changes its state
//     battery depletes

// initial queue size
const int q_init = 0;

// dummy constants
const int CMAX;
const double THRESHOLD;

// ----- synthesized parameters ------------------------------------------------

const double p32;

// queue size
const int QMAX;
// observation level thresholds
const double T1; const double T2; const double T3;
// profiles desired at observation levels
const int P1; const int P2; const int P3; const int P4;

// ----- modules ---------------------------------------------------------------


// clock

module CLOCK
    c : [0..1] init 0;
    [tick0] c=0 -> (c'=1);
    [tick1] c=1 -> (c'=0);
endmodule


// power manager

module PM
    pm  :  [0..2] init 0; // 0 - sleep, 1 - idle, 2 - active
    [tick0] q*10 <= T1*QMAX*10 -> (pm'=P1);
    [tick0] q*10 > T1*QMAX*10 & q*10 <= T2*QMAX*10 -> (pm'=P2);
    [tick0] q*10 > T2*QMAX*10 & q*10 <= T3*QMAX*10 -> (pm'=P3);
    [tick0] q*10 > T3*QMAX*10 -> (pm'=P4);
endmodule


// service provider

module SP
    sp : [0..4] init 0;
    // 0 - sleep, 1 - idle, 2 - active
    // waiting states: 3 - sleep to idle, 4 - idle to active

    // immediate transitions - change to lower-energy (or same) state
    [tick1] sp <= 2 & pm <= sp -> (sp'=pm);

    // transitions through waiting states - change to higher-energy state (sleep to idle or idle to active)
    [tick1] sp <= 2 & pm > sp -> (sp'=sp+3);

    // waiting states
    [tick1] sp = 3 -> p32 : (sp'=sp-2) + (1-p32) : true;
    [tick1] sp = 4 -> 0.6 : (sp'=sp-2) + 0.4 : true;

endmodule


// service requester

module SR
    sr : [0..1] init 0; // 0 - idle, 1 - active
    [tick1] sr=0 -> 0.5: true + 0.5: (sr' = 1);
    [tick1] sr=1 -> 0.8: true + 0.2: (sr' = 0);
endmodule


// service request queue

module SRQ
    q : [0..10000] init q_init;
    lost : [0..1] init 0;

    [tick0] sr=1 & q < QMAX -> (q'=q+1); // request
    [tick0] sr=1 & q = QMAX -> (lost'=1); // request lost
    [tick0] sr!=1 -> true;

    [tick1] sp=2 -> (lost'=0) & (q'=max(q-1,0)); // serve
    [tick1] sp!=2 -> (lost'=0);

endmodule

// battery

module BAT
    bat : [0..1] init 1; // 0 empty, 1 - operational
    [tick1] bat=0 ->true;
    [tick1] bat=1 -> 0.01 : (bat'=0) + 0.99 : true;
endmodule

// ----- rewards ----------------------------------------------------------------

label "finished" = (bat = 0);

rewards "time"
    [tick0] true : 1;
endrewards

rewards "queuesize"
    [tick0] true: q;
endrewards

rewards "requests"
    [tick0] sr=1 : 1;
endrewards

rewards "served"
    [tick1] q > 0 & sp=2 : 1;
endrewards

// rewards "lost"
//    [tick0] q=QMAX & sr=1 : 1;
// endrewards

rewards "lost"
    [tick1] lost=1 : 1;
endrewards

rewards "power"
    [tick1] sp=2 : 100 + 10*q + (1-p32)*10; // active
    [tick1] sp=3 : 10 + (1-p32)*10; // idle to active
    [tick1] sp=4 : 5 + (1-p32)*10; // sleep to idle
endrewards