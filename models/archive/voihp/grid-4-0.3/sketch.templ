

// 4x4 grid
// from Littman, Cassandra and Kaelbling
// Learning policies for partially observable environments: Scaling up  
// Technical Report CS, Brown University

pomdp


// only the target is observable which is in the south east corner
observables
	o
endobservables

const double sl=0.3;


const int M_MAX = 3;
const int X_MAX = M_MAX;
const int Y_MAX = M_MAX;

const int X_T = M_MAX;
const int Y_T = 0;

module grid
	
	x : [0..X_MAX]; // x coordinate
	y : [0..Y_MAX]; // y coordinate
	o : [0..2]; // observables
	// 0 - initial observation
	// 1 - in the grid (not target)
	// 2 - observe target
		
	// initially randomly placed within the grid (not at the target)
	[] o=0 -> 1/15 : (o'=1) & (x'=0) & (y'=0)
			+ 1/15 : (o'=1) & (x'=0) & (y'=1)
			+ 1/15 : (o'=1) & (x'=0) & (y'=2)
			+ 1/15 : (o'=1) & (x'=0) & (y'=3)
			+ 1/15 : (o'=1) & (x'=1) & (y'=0)
			+ 1/15 : (o'=1) & (x'=1) & (y'=1)
			+ 1/15 : (o'=1) & (x'=1) & (y'=2)
			+ 1/15 : (o'=1) & (x'=1) & (y'=3)	
			+ 1/15 : (o'=1) & (x'=2) & (y'=0)
			+ 1/15 : (o'=1) & (x'=2) & (y'=1)
			+ 1/15 : (o'=1) & (x'=2) & (y'=2)
			+ 1/15 : (o'=1) & (x'=2) & (y'=3)	
			// + 1/15 : (o'=1) & (x'=3) & (y'=0) this is the traget
			+ 1/15 : (o'=1) & (x'=3) & (y'=1)
			+ 1/15 : (o'=1) & (x'=3) & (y'=2)
			+ 1/15 : (o'=1) & (x'=3) & (y'=3);
			
	// move around the grid
	[east] o=1 & !(x=X_T-1 & y=Y_T) -> (1-sl): (x'=min(x+1,X_MAX)) + sl: true; // not reached target
	[east] o=1 & (x=X_T-1 & y=Y_T) -> (1-sl): (x'=min(x+1,X_MAX)) & (o'=2) + sl: true; // reached target
	[west] o=1 -> (1-sl): (x'=max(x-1,0)) + sl: (y'=min(y+1,Y_MAX));
	[north] o=1 -> (1-sl): (y'=min(y+1,Y_MAX)) + sl: (y'=min(y+1,Y_MAX));
	[south] o=1 & !(x=X_T & y=Y_T+1) -> (1-sl): (y'=max(y-1,0)) + sl: true; // not reached target
	[south] o=1 & (x=X_T & y=Y_T+1) -> (1-sl): (y'=max(y-1,0)) & (o'=2) + sl: true; // reached target
	
	// reached target
	[done] o=2 -> true;
	
endmodule

// reward structure for number of steps to reach the target
rewards "steps"
        [east] true : 1;
        [west] true : 1;
        [north] true : 1;
        [south] true : 1;
endrewards

// target observation
label "goal" = o=2;
