

// 4x4 grid
// from Littman, Cassandra and Kaelbling
// Learning policies for partially observable environments: Scaling up  
// Technical Report CS, Brown University

pomdp


// only the target is observable which is in the south east corner
observables
	o
endobservables

const double sl=0.1;


module grid
	
	x : [0..3]; // x coordinate
	y : [0..3]; // y coordinate
	o : [0..3]; // observables
	// 0 - initial observation
	// 1 - in the grid (not target)
	// 2 - observe target
	// 3 - bad state
		
	// initially randomly placed within the grid (not at the target)
	[] o=0 -> 1/14 : (o'=1) & (x'=0) & (y'=0)
			+ 1/14 : (o'=1) & (x'=0) & (y'=1)
			+ 1/14 : (o'=1) & (x'=0) & (y'=2)
			+ 1/14 : (o'=1) & (x'=0) & (y'=3)
			+ 1/14 : (o'=1) & (x'=1) & (y'=0)
			//+ 1/15 : (o'=1) & (x'=1) & (y'=1)
			+ 1/14 : (o'=1) & (x'=1) & (y'=2)
			+ 1/14 : (o'=1) & (x'=1) & (y'=3)	
			+ 1/14 : (o'=1) & (x'=2) & (y'=0)
			+ 1/14 : (o'=1) & (x'=2) & (y'=1)
			+ 1/14 : (o'=1) & (x'=2) & (y'=2)
			+ 1/14 : (o'=1) & (x'=2) & (y'=3)	
			// + 1/15 : (o'=1) & (x'=3) & (y'=0) this is the traget
			+ 1/14 : (o'=1) & (x'=3) & (y'=1)
			+ 1/14 : (o'=1) & (x'=3) & (y'=2)
			+ 1/14 : (o'=1) & (x'=3) & (y'=3);
			
	// move around the grid
	[east] o=1 & !(x=2 & y=0) &!(x=0 & y=1) -> (1-sl): (x'=min(x+1,3)) + sl: (x'=x)&(y'=y)&(o'=o); // not reached target
	[east] o=1 & x=0 & y=1 -> (1-sl): (x'=1) & (o'=3) + sl: (x'=x)&(y'=y)&(o'=o); // reached bad state
	[east] o=1 & x=2 & y=0 -> (1-sl): (x'=min(x+1,3)) & (o'=2) + sl: (x'=x)&(y'=y)&(o'=o); // reached target
	
	[west] o=1 & !(x=2 & y=1) -> (1-sl): (x'=max(x-1,0)) + sl: (x'=x)&(y'=y)&(o'=o); // not reached target
	[west] o=1 & x=2 & y=1 -> (1-sl): (x'=1) & (o'=3) + sl: (x'=x)&(y'=y)&(o'=o); // reached bad state

	[north] o=1 & !(x=1 & y=0)-> (1-sl): (y'=min(y+1,3)) + sl: (x'=x)&(y'=y)&(o'=o); // reached target
	[north]	o=1 & (x=1 & y=0) -> (1-sl): (y'=1) & (o'=3) + sl: (x'=x)&(y'=y)&(o'=o); //reached bad state
	
	[south] o=1 & !(x=3 & y=1) & !(x=1 & y=2) -> (1-sl): (y'=max(y-1,0)) + sl: (x'=x)&(y'=y)&(o'=o); // not reached target
	[south] o=1 & x=3 & y=1 -> (1-sl): (y'=max(y-1,0)) & (o'=2) + sl: (x'=x)&(y'=y)&(o'=o); // reached target
	[south] o=1 & x=1 & y=2 -> (1-sl): (y'=1) & (o'=3) + sl: (x'=x)&(y'=y)&(o'=o); //reached bad state	
	
	// reached target
	[done] o=2 -> true;
	
	//reached bad state
	[bad] o=3 -> true;
	
endmodule

// reward structure for number of steps to reach the target
rewards "steps"
        [east] true : 1;
        [west] true : 1;
        [north] true : 1;
        [south] true : 1;
endrewards

// target observation
label "goal" = o=2;
label "bad" = o=3;
